/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/


@kernel void lssAdvectionVolumeTri2D(const dlong Nelements,
                                  const  dfloat    t,
                                  @restrict const  dfloat *  vgeo,
                                  @restrict const  dfloat *  D,
                                  @restrict const  dfloat *  x,
                                  @restrict const  dfloat *  y,
                                  @restrict const  dfloat *  z,
                                  @restrict const  dfloat *  U,
                                  @restrict const  dfloat *  q,
                                  @restrict        dfloat *  rhsq){

  for(dlong e=0;e<Nelements;++e;@outer(0)){

    @shared dfloat s_F[p_Np];
    @shared dfloat s_G[p_Np];

    for(int n=0;n<p_Np;++n;@inner(0)){
      // prefetch geometric factors (constant on triangle)
      const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
      const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
      const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
      const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];

      const dlong  idq = e*p_Np + n;
      const dlong  idv = e*p_Np*p_dim + n;

      const dfloat qn = q[idq];
      const dfloat un = U[idv + 0*p_Np]; 
      const dfloat vn = U[idv + 1*p_Np]; 
      
      const dfloat qxn = qn*un;  // flux function: assumes incompressible flow field!!!
      const dfloat qyn = qn*vn;  // flux function: assumes incompressible flow field!!!

      s_F[n] = drdx*qxn + drdy*qyn;
      s_G[n] = dsdx*qxn + dsdy*qyn;
    }


    for(int n=0;n<p_Np;++n;@inner(0)){

      dfloat rhsqn=0;

      for(int i=0;i<p_Np;++i){
        const dfloat Drni = D[n+i*p_Np+0*p_Np*p_Np];
        const dfloat Dsni = D[n+i*p_Np+1*p_Np*p_Np];

        rhsqn += (Drni*s_F[i] + Dsni*s_G[i]);
      }

      // move to rhs
      const dlong id = e*p_Np + n;
      rhsq[id] = -rhsqn;
    }
  }
}


// batch process elements
@kernel void lssAdvectionSurfaceTri2D(const dlong Nelements,
                                  @restrict const  dfloat *  sgeo,
                                  @restrict const  dfloat *  LIFT,
                                  @restrict const  dlong  *  vmapM,
                                  @restrict const  dlong  *  vmapP,
                                  @restrict const  int    *  EToB,
                                            const  dfloat time,
                                  @restrict const  dfloat *  x,
                                  @restrict const  dfloat *  y,
                                  @restrict const  dfloat *  z,
                                  @restrict const  dfloat *  U,                                  
                                  @restrict const  dfloat *  q,
                                  @restrict dfloat *  rhsq){

  // for all elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockS;@outer(0)){

    // @shared storage for flux terms
    @shared dfloat s_qflux[p_NblockS][p_NfacesNfp];

    // for all face nodes of all elements
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){ // maxNodes = max(Nfp*Nfaces,Np)
        const dlong e = eo + es;
        if(e<Nelements){
          if(n<p_NfacesNfp){
            // find face that owns this node
            const int face = n/p_Nfp;

            // load surface geofactors for this face
            const dlong sid   = p_Nsgeo*(e*p_Nfaces+face);
            const dfloat nx   = sgeo[sid+p_NXID];
            const dfloat ny   = sgeo[sid+p_NYID];
            const dfloat sJ   = sgeo[sid+p_SJID];
            const dfloat invJ = sgeo[sid+p_IJID];

            // indices of negative and positive traces of face node
            const dlong id  = e*p_Nfp*p_Nfaces + n;
            const dlong idM = vmapM[id];
            const dlong idP = vmapP[id];

            // load traces
            const dfloat qM = q[idM];  dfloat qP = q[idP];

            // load traces
            const dlong eM = e;
            const dlong eP = idP/p_Np;
            const int vidM = idM%p_Np;
            const int vidP = idP%p_Np;

            const dlong qbaseM = eM*p_Np*p_dim + vidM;
            const dlong qbaseP = eP*p_Np*p_dim + vidP;

            // load negative and positive trace node values of U, V, Pr
            const dfloat  uM = U[qbaseM+0*p_Np], vM = U[qbaseM+1*p_Np];
                  dfloat  uP = U[qbaseP+0*p_Np], vP = U[qbaseP+1*p_Np];


            // apply boundary condition
            const int bc = EToB[face+p_Nfaces*e];
            if(bc>0){
              lssDirichletConditions2D(bc, time, x[idM], y[idM], nx, ny, qM, &qP);
            }

           // Assumes velocity field is not C0 i.e. solution of INS-DG 
            dfloat unM   = fabs(nx*uM + ny*vM);
            dfloat unP   = fabs(nx*uP + ny*vP);    
            dfloat unMax = (unM > unP) ? unM : unP;

            s_qflux[es][n] = 0.5*invJ*sJ*( nx*(uM*qM - uP*qP) + ny*(vM*qM - vP*qP) - unMax*(qM-qP) );
          }
        }
      }
    }

    // wait for all @shared memory writes of the previous inner loop to complete


    // for each node in the element
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){
        const dlong e = eo + es;
        if(e<Nelements){
          if(n<p_Np){
            dfloat Lqflux = 0.f;
            // rhs += LIFT*((sJ/J)*(A*nx+B*ny)*(q^* - q^-))
            #pragma unroll p_NfacesNfp
              for(int m=0;m<p_NfacesNfp;++m){
                const dfloat L = LIFT[n+m*p_Np];
                Lqflux += L*s_qflux[es][m];
              }

            const dlong id = e*p_Np+n;
            rhsq[id] += Lqflux;
          }
        }
      }
    }
  }
}